#@TYPE: Machine
#@NAME: WeTeK play
#@DESCRIPTION: Machine configuration for the WeTeK play

MACHINE_NAME = "PLAY"
BRAND_OEM = "wetek"
MACHINE_BRAND = "WeTeK"

GST_VERSION = "0.10"

# Amlogic defaults
require conf/machine/include/amlogic.inc

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "\
    wetek-dvb-modules-${MACHINE} \
    wetek-shutdown \
    enigma2-plugin-extensions-rcuselect \
    gst-amlavsink \
    gst-app \
    "

DVBPROVIDER = "kernel"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = "\
    kernel-module-cdfs \
    kernel-module-sysfillrect \
    kernel-module-syscopyarea \
    kernel-module-mceusb \
    kernel-module-rc-core \
    kernel-module-mali \
    kernel-module-dhd \
    kernel-module-CH341 \
    amlsetfb \
    amremote \
    pcsc-lite \
    "

MACHINE_EXTRA_RRECOMMENDS = " \
    libamplayer \
    libamavutils \
    "

include conf/machine/include/autoload-filesystems.inc

KERNEL_MODULE_AUTOLOAD += "\
    e2-procfs \
    sysfillrect \
    syscopyarea \
    mceusb \
    rc-core \
    mali \
    dhd \
    CH341 \
"

# Uncomment the following line to enable the hard floating point abi. Note that
# this breaks some binary libraries and 3D (neither of which ship with
# meta-yocto). For maximum compatibility, leave this disabled.
DEFAULTTUNE ?= "cortexa9hf-neon"
include conf/machine/include/tune-cortexa9.inc

IMAGE_FSTYPES += "ext4 wetek-sdimg"

# Serial console to be used
SERIAL_CONSOLE = "115200 ttyS0"

PREFERRED_PROVIDER_virtual/kernel = "linux-wetek"
PREFERRED_VERSION_linux-wetek = "3.10.76"
PREFERRED_VERSION_linux-libc-headers = "3.10"

KERNEL_IMAGETYPE = "uImage-dtb"
KERNEL_DEVICETREE = "wetekplay.dtd"

UBOOT_ENTRYPOINT  = "0x80008000"
UBOOT_LOADADDRESS = "0x80008000"

IMAGEDIR ?= "${MACHINE}"

IMAGE_CMD_wetek-sdimg_append = " \
    mkdir -p ${DEPLOY_DIR_IMAGE}/${IMAGEDIR}; \
    mv ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.wetek-sd.img ${DEPLOY_DIR_IMAGE}/${IMAGEDIR}/${IMAGE_NAME}.sdcard.img; \
    echo ${IMAGE_VER_STRING} > ${DEPLOY_DIR_IMAGE}/${IMAGEDIR}/imageversion; \
    cd ${DEPLOY_DIR_IMAGE}; \
    zip ${IMAGE_NAME}_sdcard.zip ${IMAGEDIR}/*; \
    rm -f ${DEPLOY_DIR_IMAGE}/*.ext4; \
    rm -f ${DEPLOY_DIR_IMAGE}/*.manifest; \
    rm -f ${DEPLOY_DIR_IMAGE}/.ext4; \
    rm -f ${DEPLOY_DIR_IMAGE}/.manifest; \
    rm -f ${DEPLOY_DIR_IMAGE}/.wetek-sd.img; \
    rm -f ${DEPLOY_DIR_IMAGE}/*.rootfs.wetek-sd.img; \
    rm -Rf ${IMAGEDIR}; \
    "

MACHINE_FEATURES = "bluetooth fb meson6 usbhost vfat alsa dvb-c legacykernel nolcd"

# We know about rtc
include conf/machine/include/build-extras.inc
